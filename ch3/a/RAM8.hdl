// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/RAM8.hdl

/**
 * Memory of 8 registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM8 {
    IN in[16], load, address[3];
    OUT out[16];

    PARTS:
    // Set register loads
    DMux8Way(in=load, sel=address,
             a=la, b=lb, c=lc, d=ld, e=le, f=lf, g=lg, h=lh);
    // array of 8 registers
    Register(in=in, load=la, out=reg0);
    Register(in=in, load=lb, out=reg1);
    Register(in=in, load=lc, out=reg2);
    Register(in=in, load=ld, out=reg3);
    Register(in=in, load=le, out=reg4);
    Register(in=in, load=lf, out=reg5);
    Register(in=in, load=lg, out=reg6);
    Register(in=in, load=lh, out=reg7);
    // read register
    Mux8Way16(a=reg0, b=reg1, c=reg2, d=reg3,
              e=reg4, f=reg5, g=reg6, h=reg7,
              sel=address, out=out);
}
